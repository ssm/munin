#! /usr/bin/perl
# Automatically enables "strict", "warnings", "utf8" and
# Perl 5.10 features

use Mojolicious::Lite;
use DBI;

use Munin::Common::Defaults;

my $datafilename = "$Munin::Common::Defaults::MUNIN_DBDIR/datafile.sqlite";

get '/nodes' => sub {
	my $self = shift;

	$self->render(json => get_table_data("node"));
};

get '/services' => sub {
	my $self = shift;

	$self->render(json => get_table_data("service"));
};

# Start the Mojolicious command system
app->start;

sub get_table_data
{
	my ($table) = @_;

	my $dbh = DBI->connect("dbi:SQLite:dbname=$datafilename","","") or die $DBI::errstr;
	my $nodes = $dbh->selectall_arrayref(
		"SELECT * FROM $table",
		{ Slice => {} }, # Each row is stored as a hashref
	);

	my $sth_attr = $dbh->prepare(
		"SELECT name, value FROM $table"."_attr WHERE id = ?",
	);
	foreach my $node (@$nodes) {
		$sth_attr->execute($node->{id});
		while (my ($_name, $_value) = $sth_attr->fetchrow_array) {
			$node->{attr}{$_name} = $_value;
		}
	}

	return $nodes;
}
